Map:
CamelCase           = No Terminal
"Entre comillas"    = Terminal Simple
lowercase           = Terminal con Valor
[...]               = Opcional
----------------------------------------

Program -> PreModuleList

PreModuleList -> "Sub" "Main" "(" [PreArgumentList] ")" \n StatementList \n "End" "Sub" \n ModuleList | Function \n PreModuleList | Procedure \n PreModuleList | Declaration \n PreModuleList

ModuleList -> Function \n ModuleList | Procedure \n ModuleList | Declaration \n ModuleList | " "

Function -> "Function" id ["(" PreArgumentList ")"] "As" type \n  StatementList "End" "Function"

Procedure -> "Sub" id ["(" PreArgumentList ")"] \n StatementList "End" "Sub"

PreArgumentList -> ArgumentList | " "

ArgumentList -> Argument"," ArgumentList | Argument
Argument -> id "As" type

PreStatementList -> StatementList \n | " "

FunctionCall -> id "(" [ExpressionList] ")"

StatementList -> Statement \n StatementList | Statement
Statement -> Declaration | Assignment | Conditional | Loop | IO | FunctionCall | " "

Declaration -> "Dim" id "As" "Type" ["=" Expression]
Assignment -> id "=" Expression

ExpressionList -> Expression"," ExpressionList | Expression

Expression -> Expression additionOperator Expression 
            | Expression multiplicationOperator Expression 
            | Expression relationalOperator Expression
            | Expression "AND" Expression
            | Expression "OR" Expression
            | "NOT" Expression
            | id 
            | integer 
            | string 
            | string concatenate string
            | char
            | id "("[ExpressionList]")"
            | "(" Expression ")"

IO -> "PRINT" Expression | "ReadLine" id

Conditional -> "If" Expression "Then" \n PreStatementList \n [ConditionalElse] "End" "If"
ConditionalElse -> "Else" \n PreStatementList
                 | "ElseIf" Expression "Then" \n PreStatementList [ConditionalElse]

Loop -> For | While

For -> "For" Assignment "To" integer \n PreStatementList "Next"

While -> "Do" \n PreStatementList "Loop" "While" Expression